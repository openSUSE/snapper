
AC_INIT(snapper/Snapper.h)

VERSION=`cat ./VERSION`

LIBVERSION=`cat ./LIBVERSION`
LIBVERSION_MAJOR=`cut -d . -f 1 ./LIBVERSION`
LIBVERSION_MINOR=`cut -d . -f 2 ./LIBVERSION`
LIBVERSION_PATCHLEVEL=`cut -d . -f 3 ./LIBVERSION`

LIBVERSION_CURRENT=`expr $LIBVERSION_MAJOR + $LIBVERSION_MINOR`
LIBVERSION_REVISON=`expr $LIBVERSION_PATCHLEVEL`
LIBVERSION_AGE=`expr $LIBVERSION_MINOR`
LIBVERSION_INFO=$LIBVERSION_CURRENT:$LIBVERSION_REVISON:$LIBVERSION_AGE

AM_INIT_AUTOMAKE(snapper, $VERSION)
AM_CONFIG_HEADER(config.h)

AC_DISABLE_STATIC

AC_PROG_CXX
AC_PROG_LIBTOOL
AM_PROG_LIBTOOL

AC_PREFIX_DEFAULT(/usr)

dnl Automake 1.11 enables silent compilation
dnl Disable it by "configure --disable-silent-rules" or "make V=1"
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

CFLAGS="${CFLAGS} -Wall -Wformat=2 -Wmissing-prototypes"
CXXFLAGS="${CXXFLAGS} -std=c++0x -DHAVE_CXX0X -Wall -Wextra -Wformat=2 -Wnon-virtual-dtor -Wno-unused-parameter"

fillupdir=/var/adm/fillup-templates

AC_ARG_ENABLE([btrfs], AC_HELP_STRING([--disable-btrfs],[Disable Btrfs internal snapshots support]),
		[with_btrfs=$enableval],[with_btrfs=yes])

AM_CONDITIONAL(ENABLE_BTRFS, [test "x$with_btrfs" = "xyes"])

if test "x$with_btrfs" = "xyes"; then
	AC_DEFINE(ENABLE_BTRFS, 1, [Enable Btrfs internal snapshots support])
fi

AC_ARG_ENABLE([ext4], AC_HELP_STRING([--disable-ext4],[Disable ext4 snapshots support]),
		[with_ext4=$enableval],[with_ext4=yes])

AM_CONDITIONAL(ENABLE_EXT4, [test "x$with_ext4" = "xyes"])

if test "x$with_ext4" = "xyes"; then
	AC_DEFINE(ENABLE_EXT4, 1, [Enable Ext4 snapshots support])
fi

AC_ARG_ENABLE([lvm], AC_HELP_STRING([--disable-lvm],[Disable LVM thinprovisioned snapshots support]),
		[with_lvm=$enableval],[with_lvm=yes])

AM_CONDITIONAL(ENABLE_LVM, [test "x$with_lvm" = "xyes"])

if test "x$with_lvm" = "xyes"; then
	AC_DEFINE(ENABLE_LVM, 1, [Enable LVM thin-provisioned snapshots support])
fi

if test "x$with_btrfs" != "xyes" -a "x$with_lvm" != "xyes" -a "x$with_ext4" != "xyes"; then
	AC_MSG_ERROR([You have to enable at least one snapshot type (remove some --disable-xxx parameter)])
fi

AC_ARG_ENABLE([zypp], AC_HELP_STRING([--disable-zypp],[Disable zypp plugin support]),
		[with_zypp=$enableval],[with_zypp=yes])
AM_CONDITIONAL(HAVE_ZYPP, [test "$with_zypp" != "no"])

PKG_CHECK_MODULES(DBUS, dbus-1)

AC_SUBST(VERSION)
AC_SUBST(LIBVERSION_MAJOR)
AC_SUBST(LIBVERSION_INFO)
AC_SUBST(docdir)
AC_SUBST(fillupdir)

AC_OUTPUT(
	Makefile
	snapper/Makefile
	examples/Makefile
	examples/C/Makefile
	dbus/Makefile
	server/Makefile
	client/Makefile
	client/utils/Makefile
	scripts/Makefile
	data/Makefile
	doc/Makefile
	doc/snapper.8:doc/snapper.8.in
	doc/snapperd.8:doc/snapperd.8.in
	po/Makefile
	testsuite-real/Makefile
	package/snapper.spec:snapper.spec.in
)
